apply plugin: 'com.android.library'

def getVersionName() {
    return "4.15.0"
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 27
        versionCode 1
        versionName getVersionName()
        consumerProguardFiles 'adjust-proguard-rules.txt'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

task makeReleaseJar(type: Copy) {
    dependsOn 'build'

    from('build/intermediates/intermediate-jars/release/')
    into('build/outputs/jar/')
    include('classes.jar')
    rename ('classes.jar', "adjust-${getVersionName()}-release.jar")
}

task makeSonatypeJar(type: Copy) {
    dependsOn 'build'

    from('build/intermediates/intermediate-jars/release/')
    into('build/outputs/sonatype-jars/')
    include('classes.jar')
    rename ('classes.jar', "adjust-android-${getVersionName()}.jar")
}

task makeDebugJar(type: Copy) {
    dependsOn 'build'

    from('build/intermediates/intermediate-jars/debug/')
    into('build/outputs/jar/')
    include('classes.jar')
    rename ('classes.jar', "adjust-${getVersionName()}-debug.jar")
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // JDK 1.8 is more strict then 1.7. Have JDK 1.8 behave like 1.7 for javadoc generation
    if (org.gradle.internal.jvm.Jvm.current().getJavaVersion() == JavaVersion.VERSION_1_8) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task androidJavadocsJar(type: Jar) {
    dependsOn 'androidJavadocs'

    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task makeSonatypeJavadocJar(type: Copy) {
    dependsOn 'androidJavadocsJar'

    from('build/libs/')
    into('build/outputs/sonatype-jars/')
    include('adjust-javadoc.jar')
    rename ('adjust-javadoc.jar', "adjust-android-${getVersionName()}-javadoc.jar")
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task makeSonatypeSourcesJar(type: Copy) {
    dependsOn 'androidSourcesJar'

    from('build/libs/')
    into('build/outputs/sonatype-jars/')
    include('adjust-sources.jar')
    rename ('adjust-sources.jar', "adjust-android-${getVersionName()}-sources.jar")
}

task makeSonatypeBundleJar() {
    dependsOn 'makeSonatypeJar'
    dependsOn 'makeSonatypeJavadocJar'
    dependsOn 'makeSonatypeSourcesJar'

}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}
