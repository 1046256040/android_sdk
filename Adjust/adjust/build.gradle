apply plugin: 'com.android.library'

def getVersionName() {
    return "4.15.1"
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 27
        versionCode 1
        versionName getVersionName()
        consumerProguardFiles 'adjust-proguard-rules.txt'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
}

task makeReleaseJar(type: Copy) {
    dependsOn 'build'

    from('build/intermediates/intermediate-jars/release/')
    into('build/outputs/jar/')
    include('classes.jar')
    rename ('classes.jar', "adjust-${getVersionName()}-release.jar")
}

task makeDebugJar(type: Copy) {
    dependsOn 'build'

    from('build/intermediates/intermediate-jars/debug/')
    into('build/outputs/jar/')
    include('classes.jar')
    rename ('classes.jar', "adjust-${getVersionName()}-debug.jar")
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}
