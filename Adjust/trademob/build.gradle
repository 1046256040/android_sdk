apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/staging/"
    }
}

android {
    compileSdkVersion rootProject.ext.coreCompileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.coreMinSdkVersion
        targetSdkVersion rootProject.ext.coreTargetSdkVersion
        versionName rootProject.ext.coreVersionName
        versionCode rootProject.ext.defaultVersionCode
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Add SDK via module.
    implementation project(":adjust")
    // Add SDK via Maven.
    //implementation 'com.adjust.sdk:adjust-android:4.15.2'
}

task adjustTrademobAndroidJar(type: Jar) {
    from('build/intermediates/classes/release/')
    archiveName "${project.name}.jar"
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // JDK 1.8 is more strict then 1.7. Have JDK 1.8 behave like 1.7 for javadoc generation
    if (org.gradle.internal.jvm.Jvm.current().getJavaVersion() == JavaVersion.VERSION_1_8) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

task adjustTrademobAndroidJavadocsJar(type: Jar) {
    dependsOn 'androidJavadocs'

    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task adjustTrademobAndroidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives adjustTrademobAndroidJar
    archives adjustTrademobAndroidJavadocsJar
    archives adjustTrademobAndroidSourcesJar
}

publishing {
    publications {
        mavenAndroidTrademob(MavenPublication) {
            customizePom(pom)
            groupId rootProject.ext.adjustGroupId
            artifactId 'adjust-android-trademob'
            version rootProject.ext.coreVersionName

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                // exclude "usual" archive artifact .aar
                def signFileName = it.toString()
                if (signFileName.contains('aar')) {
                    return
                }
                // create a maven artifact for each asc signature
                artifact(it) {
                    //println "archive name ${signFileName}"
                    if (signFileName.contains('-sources')) {
                        classifier = 'sources'
                        extension = 'jar.asc'
                    } else if (signFileName.contains('-javadoc')) {
                        classifier = 'javadoc'
                        extension = 'jar.asc'
                    } else {
                        classifier = null
                        extension = 'jar.asc'
                    }
                }
            }

            artifact adjustTrademobAndroidJar
            artifact adjustTrademobAndroidJavadocsJar
            artifact adjustTrademobAndroidSourcesJar
        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'The Trademob plugin for Adjust SDK for Android'
            name 'Adjust Android SDK Trademob plugin'
            url 'https://github.com/adjust/android_sdk'

            organization {
                name 'adjust GmbH'
                url 'http://www.adjust.com'
            }

            licenses {
                license {
                    name 'MIT License'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                }
            }
            scm {
                url 'git@github.com:adjust/android_sdk.git'
                connection 'scm:git:git@github.com:adjust/android_sdk.git'
                developerConnection 'scm:git:git@github.com:adjust/android_sdk.git'
            }
            developers {
                developer {
                    name 'Pedro Silva'
                    email 'pedro@adjust.com'
                }

                developer {
                    name 'Ugljesa Erceg'
                    email 'ugljesa@adjust.com'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenAndroidTrademobPublication {
        destination = file("${project.buildDir}/generated-pom.xml")
    }
    tasks.publishMavenAndroidTrademobPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenAndroidTrademobPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}

signing {
    sign configurations.archives
}
